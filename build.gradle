buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

apply plugin: 'maven'

repositories {
    maven {
        name = 'EnndsCore'
        url = "http://exia.ollieread.com/maven/"
    }
}

dependencies {
    compile "com.ollieread.ennds:EnndsCore:${config.version.minecraft}-${config.version.ennds}:deobf"
}

version = "${config.version.minecraft}-${config.version.forge}_${config.version.major}.${config.version.minor}.${config.version.revis}${config.version.release}"
group = "com.ollieread.technomagi" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "TechnoMagi"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
    assetDir = "run/assets"
    
    replaceIn "common/References.java"
    replace "@VERSION@", "${config.version.major}.${config.version.minor}.${config.version.revis}${config.version.release}-${System.getenv("BUILD_NUMBER") ?: 0}"
}

version += "-${System.getenv("BUILD_NUMBER") ?: 0}"

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn('deobfJar')

processResources {
    from (sourceSets.main.resources.srcDirs)
}

artifacts {
    archives deobfJar 
    archives jar
}